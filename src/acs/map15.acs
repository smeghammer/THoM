#library "map15.acs"
#include "zcommon.acs"

/*
SCRIPT OPEN:
---------
*/

script 100 OPEN{
	/* set all fences blocking */
	int blockingfences[4] = {1,2,3,4};
	int blockingfencescount = 4;
	
	for(int a=0;a<blockingfencescount;a++){
		acs_execute(3,0,blockingfences[a],1);
		delay(1);
	}
	
	/* align tele gates */
	int offsetInt = 16;
	//int offsetFixed = myCoolInteger << 16; //Result is 4718592
	//see https://zdoom.org/wiki/Fixed_point_number
	Sector_SetFloorPanning(82,offsetInt,0,offsetInt,0);
	Sector_SetFloorPanning(69,offsetInt,0,offsetInt,0);
}


/*
SCRIPT 1:
---------
*/
script 1 (void){
	Floor_LowerByValue(1,12,120);
	/* Delay(210);
	Floor_RaiseByValue(1,12,120); */
	
	/* fullbright switch faces */
}

script 2 (int impaledCorpsesToGo, int spawnSpotId,int spawntype){
	/* remove the impaled corpses: */
	Thing_Remove(impaledCorpsesToGo);
	/*print(s:"remove corpses: ",i:impaledCorpsesToGo);
	print(s:"spawn at: ",i:spawnSpotId);
	print(s:"spawn type: ",i:spawntype);*/
	
	/* TEST - Replace with a randomised monster (get stronger as progress?) at relevant spawnspot */
	int thingsToSpawn[4] = {T_ZOMBIE,T_IMP,T_SHOTGUY,T_CHAINGUY};
	int thingsToSpawn2[4] = {T_LOSTSOUL,T_CACODEMON,T_DEMON,T_SPECTRE};
	int fireToSpawn[3] = {10001,10002,10003};	/* custom fire thing TIDs in DECORATE file */
	
	if(spawntype == 0){
		Thing_Spawn(spawnSpotId, thingsToSpawn[Random(0,3)], 64);
	}
	if(spawntype == 1){
		Thing_Spawn(spawnSpotId, fireToSpawn[Random(0,2)], 64);
	}
	if(spawntype == 2){
		Thing_Spawn(spawnSpotId, thingsToSpawn2[Random(0,3)], 64);
	}
	
	/* finally, remove the specified spawnspot so we don't retrigger */
	/* print(s:"removing spawn spot #",i:spawnSpotId); */
	Thing_Remove(spawnSpotId);
	
	/* TODO: have multiple lines that trigger release of staked corpses - have counter on a complex array to
	keep track of whether a line has been triggered. I've done this before somewhere... */
}

/* steps and roof line blocking logic */
script 3 (int lineid, int blockingstate,int checkheightmapid){
	/* log(s:"blockingstate", i:blockingstate);
	print(s:"Player height: ",i:GetActorZ(0)/65536); */
	int heightcheckmap[3] = {0,-1012,-840};
	
	/* if activeheight is passed, we test actor height and only allow the line blocking toggle to run if the user is at this height (=/-16) */
	int checkHeight = 1;
	if(checkheightmapid > 0){	/* some lines don't need testing */
		delay(1);
		/* print(s:"checking height:",i:GetActorZ(0)/65536); */
		//delay(1);
		//print(s:"height: ",i:(GetActorZ(0)/65536));
		//delay(1);
		//print(s:"passed index: : ",i:checkheightmapid);
		//delay(1);
		//print(s:"active height: ",i:heightcheckmap[checkheightmapid]);
		checkHeight = 0;
		if( 
			(GetActorZ(0)/65536) <= heightcheckmap[checkheightmapid] + 32 
			&& 
			(GetActorZ(0)/65536) >= heightcheckmap[checkheightmapid] - 32
			){
		//if(  (heightcheckmap[checkheightmapid] - 32) <= (GetActorZ(0)/65536) <= (heightcheckmap[checkheightmapid] + 32) ){
			/* print(s:"unblock"); */
			checkHeight = 1;
		}
		//else{
		//	//debug
		//	print(s:"don't unblock");
		//}
	}

	
	if(checkHeight == 1){
		if(blockingstate == 1){
			/* print(s:"blocking line ",i:lineid); */
			//Line_SetBlocking(lineid,32,0);	all
			Line_SetBlocking(lineid,64,0);
		}
		else{
			/* unblock */
			/* print(s:"unblocking line ",i:lineid); */
			//Line_SetBlocking(lineid,0,32);	//all
			Line_SetBlocking(lineid,0,64);		//mid
		}
	}
}

/* scripted 3d door. Move control sector 'tag' by 'distance' to simulate doors with 3D sectors */
script 4(int tag, int distance, int stayopen){
	/* 
	Prevent logical door height errors:
	 - if 'stayopen', activators MUST be s1/w1 etc.
	 - if repeatable door, need to check initial height and prevent retrigger until returned, otherwise
	   the door can be lowered into the floor...
	*/
	
	int _controlSectorStartingHeight = (GetSectorFloorZ(tag,0,0)/65536);
	//print(s:"control sector floor height: ",i:_controlSectorStartingHeight);

	//use the same TID for soundspot
	floorandceiling_raisebyvalue(tag,12,distance);
	ThingSound(40,"doors/dr1_open",127);
	//TagWait (tag);
	
	delay(108);	//allow time to finish
	//print(i:stayopen);
	//print(s:"opened door height: ",i:((GetSectorFloorZ(tag,0,0)/65536)));
	/* only trigger if the initial height == starting height. This prevents logical errors when triggering the door again if it has not returned to the correct height  */
	if(stayopen == 0 && (GetSectorFloorZ(tag,0,0)/65536) == _controlSectorStartingHeight+distance){
		floorandceiling_lowerbyvalue(tag,12,distance);
		ThingSound(40,"doors/dr1_clos",127);
	}
}

script 5 (int msgindex){
	int msgs[3] = {
	"Blocked... Must be opened elsewhere...",
	"This must open those bars...",
	"Must have missed a switch back there..."
	};
	print(s:msgs[msgindex]);
}

/* lower bars to level 3: */
script 6(int floorToLower,int dist,int _return){
	Floor_LowerByValue(floorToLower,12,dist);
	if(_return > 0){
		Delay(210);
		Floor_RaiseByValue(floorToLower,12,dist);
	}
}

/* better scripted 3d moving platform. Move control sector 'tag' by 'distance' to simulate doors with 3D sectors. Also has a delay mapper to delay by
correct amnt for height.

This iteration uses a mapper key to remove the  need for height args
also, see https://github.com/rheit/zdoom/blob/master/wadsrc/static/filter/game-doomchex/sndinfo.txt
 */
script 7(int tag, int mapperindex, int stayopen){
	int delaymapper[2][2] = {{64,108},{368,432}};
	int counter = 0;
	int doormove = delaymapper[mapperindex][0];

	int doorwait = 36;
	/* Prevent logical door height errors:
	 - if 'stayopen', activators MUST be s1/w1 etc.
	 - if repeatable door, need to check initial height and prevent retrigger until returned, otherwise
	   the door can be lowered into the floor... */
	
	int _controlSectorStartingHeight = (GetSectorFloorZ(tag,0,0)/65536);
	//use the same TID for soundspot
	floorandceiling_raisebyvalue(tag,12,doormove);
	
	/* use lift sound if tall platform */
	str _sound_open = "doors/dr1_open";
	str _sound_mid = "";
	str _sound_close = "doors/dr1_clos";
	if(mapperindex == 1){	//tall
		_sound_open = "plats/pt1_strt";
		_sound_close = "plats/pt1_stop";
		_sound_mid = "plats/pt1_mid";
	}

	ThingSound(40,_sound_open,127);
	if(mapperindex == 1){
		ThingSound(40,_sound_mid,127);
		TagWait(tag);
		ThingSound(40,_sound_close,127);
	}
	/* wait for action to complete */
	TagWait(tag);
	delay(doorwait);	//allow time to finish
	/* only trigger if the initial height == starting height. This prevents logical errors when triggering the door again if it has not returned to the correct height  */
	if(stayopen == 0 && (GetSectorFloorZ(tag,0,0)/65536) == _controlSectorStartingHeight+doormove){
		floorandceiling_lowerbyvalue(tag,12,doormove);
		if(mapperindex == 1){
			ThingSound(40,_sound_open,127);
			PlaySound(40,_sound_mid,127,1,true,1,0);
			TagWait(tag);
			ThingSound(40,_sound_close,127);
		}
		else{
			ThingSound(40,_sound_close,127);
		}
	}
}

